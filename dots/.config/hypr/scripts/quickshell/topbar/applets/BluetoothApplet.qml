import QtQuick
import QtQuick.Effects
import Quickshell

Rectangle {
    id: root
    property var bar
    property var barZone
    property bool editMode: false

    property bool isHovered: btMouse.containsMouse

    radius: bar.s(14)
    border.width: 0
    color: "transparent"

    // Hidden on desktop (no BT indicator needed)
    property real targetW: bar.isDesktop ? 0 : (btRow.width + bar.s(24))
    implicitWidth:  targetW
    implicitHeight: bar.barHeight
    visible: targetW > 0
    clip: true

    Behavior on targetW   { NumberAnimation { duration: 240; easing.type: Easing.OutQuint } }
    Behavior on color     { ColorAnimation  { duration: 200 } }

    Rectangle {
        anchors.fill: parent
        radius: bar.s(14)
        opacity: bar.isBtOn ? 1.0 : 0.0
        Behavior on opacity { NumberAnimation { duration: 180 } }
        gradient: Gradient {
            orientation: Gradient.Horizontal
            GradientStop { position: 0.0; color: bar.mauve }
            GradientStop { position: 1.0; color: Qt.lighter(bar.mauve, 1.3) }
        }
    }

    scale: btMouse.pressed ? 0.95 : (isHovered ? 1.04 : 1.0)
    Behavior on scale { NumberAnimation { duration: 130; easing.type: Easing.OutCubic } }

    Row {
        id: btRow
        anchors.centerIn: parent
        spacing: bar.s(8)

        layer.enabled: true
        layer.effect: MultiEffect {
            shadowEnabled: true
            shadowColor: Qt.rgba(0, 0, 0, 0.55)
            shadowBlur: 0.5
            shadowHorizontalOffset: 0
            shadowVerticalOffset: 1
        }
        Text {
            anchors.verticalCenter: parent.verticalCenter
            text: bar.btIcon
            font.family: "Iosevka Nerd Font"
            font.pixelSize: bar.s(16)
            color: bar.isBtOn ? bar.base : barZone.adaptiveSubtext
        }
        Text {
            anchors.verticalCenter: parent.verticalCenter
            text: bar.btDevice
            visible: text !== ""
            font.family: "JetBrains Mono"
            font.pixelSize: bar.s(13)
            font.weight: Font.DemiBold
            color: bar.isBtOn ? bar.base : barZone.adaptiveText
            width: Math.min(implicitWidth, bar.s(100))
            elide: Text.ElideRight
        }
    }

    MouseArea {
        id: btMouse
        anchors.fill: parent
        hoverEnabled: true
        enabled: !root.editMode
        onClicked: Quickshell.execDetached(["bash", "-c", "~/.config/hypr/scripts/qs_manager.sh toggle network bt"])
    }
}
