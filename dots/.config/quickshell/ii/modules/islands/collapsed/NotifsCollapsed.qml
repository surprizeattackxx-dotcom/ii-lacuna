import QtQuick
import QtQuick.Layouts
import Quickshell
import "../pet"

Row {
    property var island
    property int preferredWidth: {
        let max = Screen.width - island.s(32)
        let n = island.notifHistory.count > 0 ? island.notifHistory.get(0)
              : (island.notifActive ? island.notifData : null)
        let txt = n ? (n.title || n.body || n.appName || "") : ""
        let len = Math.min(18, txt.length)
        return Math.min(Math.max(island.s(280), island.s(200) + len * island.s(7)), max)
    }
    spacing: island.s(14)

    // Notif icon
    Rectangle {
        width: island.s(28); height: island.s(28); radius: island.s(8); clip: true
        color: Qt.rgba(island.peach.r, island.peach.g, island.peach.b, 0.15)
        anchors.verticalCenter: parent.verticalCenter
        Image {
            id: notifIcon; anchors.fill: parent; anchors.margins: island.s(5)
            fillMode: Image.PreserveAspectFit; asynchronous: true

            property string iconName: {
                let ic = island.notifHistory.count > 0 ? (island.notifHistory.get(0).icon || "")
                       : (island.notifData ? (island.notifData.icon || "") : "")
                return ic
            }
            readonly property bool iconIsPath: iconName !== "" && (
                iconName.startsWith("/") || iconName.startsWith("file://") || iconName.startsWith("http"))
            property int iconTry: 0
            onIconNameChanged: iconTry = 0

            source: {
                if (iconName === "") return ""
                if (iconIsPath)     return iconName
                switch (iconTry) {
                case 0: return "image://theme/" + iconName
                case 1: return "file:///var/lib/flatpak/exports/share/icons/hicolor/128x128/apps/" + iconName + ".png"
                case 2: return "file:///var/lib/flatpak/exports/share/icons/hicolor/256x256/apps/"  + iconName + ".png"
                case 3: return "file:///var/lib/flatpak/exports/share/icons/hicolor/scalable/apps/" + iconName + ".svg"
                default: return ""
                }
            }
            onStatusChanged: {
                if (status === Image.Error && !iconIsPath && iconTry < 3) iconTry++
            }
        }
        Text {
            visible: notifIcon.status !== Image.Ready
            anchors.centerIn: parent; text: "󰵙"
            font.family: "Iosevka Nerd Font"; font.pixelSize: island.s(14); color: island.peach
        }
    }

    // App name + title
    ColumnLayout {
        spacing: -2; anchors.verticalCenter: parent.verticalCenter
        Text {
            text: {
                let n = island.notifHistory.count > 0 ? island.notifHistory.get(0)
                      : (island.notifActive ? island.notifData : null)
                return n ? (n.appName || "System") : "System"
            }
            font.family: "JetBrains Mono"; font.pixelSize: island.s(10); font.weight: Font.Medium
            color: island.peach; elide: Text.ElideRight; Layout.maximumWidth: island.s(160)
        }
        Text {
            text: {
                let n = island.notifHistory.count > 0 ? island.notifHistory.get(0)
                      : (island.notifActive ? island.notifData : null)
                return n ? (n.title || n.body || "") : ""
            }
            font.family: "JetBrains Mono"; font.pixelSize: island.s(13); font.weight: Font.Black
            color: island.text; elide: Text.ElideRight; Layout.maximumWidth: island.s(160)
        }
    }

    CatPill {
        width: island.s(24); height: island.s(24); anchors.verticalCenter: parent.verticalCenter
        playing: island.musicData.status === "Playing"
        notifActive: island.notifActive || island.notifBadgeVisible
        showQuestion: !island.notifActive && !island.notifBadgeVisible
        catColor: island.text; eyeColor: island.base; accentColor: island.mauve
    }
}
